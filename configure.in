dnl
Mnl Configure script for the Gnome library
dnl

AC_INIT(copyright.txt)
AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(libgtop, 0.01)

AM_MAINTAINER_MODE

AM_ACLOCAL_INCLUDE(macros)

AC_CANONICAL_HOST

AC_CHECK_TOOL(CC,gcc)
AC_CHECK_TOOL(CXX,g++)
AC_CHECK_TOOL(RANLIB,ranlib)
AC_CHECK_TOOL(LD,ld)
AC_CHECK_TOOL(AS,as)
AC_CHECK_TOOL(AR,ar)
AC_CHECK_TOOL(NM,nm)

NM=`which $NM`
export CC CXX RANLIB LD AS AR NM

AC_LC_SYSDEPS

if test x$libgtop_want_guile = xyes ; then
  GNOME_CHECK_GUILE
  if test x$ac_cv_guile_found = xyes ; then
    if test x$libgtop_want_names = xyes ; then
      AC_DEFINE(GLIBTOP_GUILE_NAMES)
    fi
    guile_examples='third third_static third_linux'
    guile_subdirs='guile'
  else
    guile_examples=
    guile_subdirs=
  fi
else
  guile_examples=
  guile_subdirs=
fi

AC_SUBST(guile_examples)
AC_SUBST(guile_subdirs)

AC_ISC_POSIX
AC_PROG_CXX
AC_PROG_CC
AC_PROG_CPP
AC_STDC_HEADERS
AC_ARG_PROGRAM
AM_PROG_LEX
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

export cross_compiling
AM_PROG_LIBTOOL

GNOME_SUPPORT_CHECKS

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_STRUCT_ST_RDEV
AC_HEADER_TIME
AC_STRUCT_TM
AC_TYPE_UID_T

dnl For SunOS
AC_CHECK_TYPE(ssize_t, int)
AC_CHECK_HEADERS(memory.h)
AC_CHECK_HEADERS(string.h strings.h, break)
AC_REPLACE_FUNCS(strerror)
AC_CHECK_LIB(kvm, kvm_open)

dnl For DEC OSF1
AC_CHECK_LIB(mach, vm_statistics)

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_FUNC_MMAP
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_CHECK_FUNCS(getcwd gettimeofday getwd putenv strdup strtoul uname)

ALL_LINGUAS=""
AM_GNU_GETTEXT

CFLAGS="$CFLAGS -D_IN_LIBGTOP"

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

AC_ARG_ENABLE(debug, [  --enable-debug          turn on debugging [default=no]],AC_DEFINE(GNOME_ENABLE_DEBUG),)

LIBSUPPORT=
SUPPORTINCS=
if test "$need_gnome_support" = yes; then
   LIBSUPPORT='$(top_builddir)/support/libgnomesupport.la'
   SUPPORTINCS='-I$(top_srcdir)/support'
fi

dnl These definitions are expanded in make.
LIBGTOP_LIBS='-L$(libdir)'
LIBGTOP_INCS='-I$(includedir)'
LIBGTOP_GUILE_LIBS="$LIBGTOP_LIBS"
LIBGTOP_GUILE_INCS="$LIBGTOP_INCS"
LIBGTOP_BINDIR='$(bindir)'
LIBGTOP_SERVER='$(bindir)/gtop_server'

sysdeps_dir="$libgtop_sysdeps_dir"
AC_SUBST(sysdeps_dir)

if test x$ac_cv_guile_found = xyes ; then
  LIBGTOP_GUILE_LIBS="$LIBGTOP_GUILE_LIBS -lgtop_guile"
  libgtop_guile_found=yes
else
  libgtop_guile_found=no
fi

AC_SUBST(libgtop_guile_found)

if test x$libgtop_want_names = xyes ; then
  if test x$ac_cv_guile_found = xyes ; then
    LIBGTOP_GUILE_LIBS="$LIBGTOP_GUILE_LIBS -lgtop_guile_names"
    libgtop_want_guile_names=yes
  else
    libgtop_want_guile_names=no
  fi
  LIBGTOP_LIBS="$LIBGTOP_LIBS -lgtop_names"
  LIBGTOP_GUILE_LIBS="$LIBGTOP_GUILE_LIBS -lgtop_names"
fi

AC_SUBST(libgtop_want_guile_names)

LIBGTOP_LIBS="$LIBGTOP_LIBS -lgtop_common"
LIBGTOP_GUILE_LIBS="$LIBGTOP_GUILE_LIBS -lgtop_common"

if test "x$need_server" = "xyes" ; then
  LIBGTOP_LIBS="$LIBGTOP_LIBS -lgtop"
  LIBGTOP_GUILE_LIBS="$LIBGTOP_GUILE_LIBS -lgtop"
else
  LIBGTOP_LIBS="$LIBGTOP_LIBS -lgtop_sysdeps"
  LIBGTOP_GUILE_LIBS="$LIBGTOP_GUILE_LIBS -lgtop_sysdeps"
fi

if test "x$ac_cv_guile_found" = "xyes" ; then
  LIBGTOP_GUILE_INCS="$LIBGTOP_INCS $GUILE_INCS"
  LIBGTOP_GUILE_LIBS="$LIBGTOP_GUILE_LIBS $GUILE_LIBS"
fi

AC_SUBST(LIBGTOP_LIBS)
AC_SUBST(LIBGTOP_INCS)
AC_SUBST(LIBGTOP_GUILE_LIBS)
AC_SUBST(LIBGTOP_GUILE_INCS)
AC_SUBST(LIBGTOP_BINDIR)
AC_SUBST(LIBGTOP_SERVER)

AC_SUBST(LIBSUPPORT)
AC_SUBST(SUPPORTINCS)

AC_OUTPUT([
Makefile
po/Makefile.in
intl/Makefile
include/Makefile
include/glibtop/Makefile
sysdeps/Makefile
sysdeps/common/Makefile
sysdeps/names/Makefile
sysdeps/guile/Makefile
sysdeps/guile/names/Makefile
sysdeps/stub/Makefile
sysdeps/sun4/Makefile
sysdeps/osf1/Makefile
sysdeps/linux/Makefile
src/Makefile
src/server/Makefile
lib/Makefile
examples/Makefile
support/Makefile
macros/Makefile],[sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])
